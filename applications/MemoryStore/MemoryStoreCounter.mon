// Copyright (c) 2017-2018 Software AG, Darmstadt, Germany and/or its licensors
//
// Licensed under the Apache License, Version 2.0 (the "License"); you may not use this 
// file except in compliance with the License. You may obtain a copy of the License at
// http://www.apache.org/licenses/LICENSE-2.0
// Unless required by applicable law or agreed to in writing, software distributed under the
// License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, 
// either express or implied. 
// See the License for the specific language governing permissions and limitations under the License.

using com.apama.memorystore.Schema;
using com.apama.memorystore.Storage;
using com.apama.memorystore.Store;
using com.apama.memorystore.Table;
using com.apama.memorystore.Row;
using com.apama.memorystore.Finished;

/** Simple demo application that increments a counter in an on-disk MemoryStore table each time this monitor is loaded */
monitor Counter {
	Store store;
	Table tbl;
	
	event SampleSchema {
		integer counter;
	}

	/** Prepare the store and table */
	action onload() {
		integer id := Storage.prepareOrCreate("counterstore", "counterstore.dat"); 

		on Finished(id, true, *) {
			store := Storage.open("counterstore");

			integer id := store.prepare("countertable", Schema.schemaFromAny(new SampleSchema));
			on Finished(id, true, *) {
				tbl := store.open("countertable");
				allReady();
			}
		}
		on all Finished(*, false, *) as f {
			log "MemoryStore error: "+f.status at ERROR;
		}
	}

	/** Called when the store and table are ready for read/write
		Reads the counter and increments it in the on-disk table
	*/
	action allReady() {
		Row r := tbl.get("default");

		integer value := r.getInteger("counter");
		log "Counter table contains value: " + value.toString() at INFO;

		r.setInteger("counter", value + 1);
		r.commit();
		integer id := tbl.persist();
		on Finished(id, *, *) {
			log "Incremented counter" at INFO;
		}
	}
}
